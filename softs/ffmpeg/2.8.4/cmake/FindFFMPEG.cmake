# Find FFMPEG
#
# FFMPEG_INCLUDE_DIRS
# FFMPEG_LIBRARIES
# FFMPEG_FOUND
#
# FFMPEG_${COMPONENT}_INCLUDE_DIR
# FFMPEG_${COMPONENT}_LIBRARY
#

SET(FFMPEG_COMPONENTS
    avcodec
    avdevice
    avfilter
    avformat
    avutil
    postproc
    swscale
)

FOREACH(COMPONENT ${FFMPEG_COMPONENTS})
    STRING(TOLOWER ${COMPONENT} LOWERCOMPONENT)
    STRING(TOUPPER ${COMPONENT} UPPERCOMPONENT)

    IF(LOWERCOMPONENT STREQUAL "postproc")
        FIND_PATH(FFMPEG_${UPPERCOMPONENT}_INCLUDE_DIR NAMES libpostproc/postprocess.h)
    ELSE(LOWERCOMPONENT STREQUAL "postproc")
        FIND_PATH(FFMPEG_${UPPERCOMPONENT}_INCLUDE_DIR NAMES lib${LOWERCOMPONENT}/${LOWERCOMPONENT}.h)
    ENDIF(LOWERCOMPONENT STREQUAL "postproc")

    FIND_LIBRARY(FFMPEG_${UPPERCOMPONENT}_LIBRARY NAMES ${LOWERCOMPONENT})

    SET(${FFMPEG_${UPPERCOMPONENT}_FOUND} 1)

    MARK_AS_ADVANCED(FFMPEG_${UPPERCOMPONENT}_INCLUDE_DIR FFMPEG_${UPPERCOMPONENT}_LIBRARY FFMPEG_${UPPERCOMPONENT}_FOUND)
ENDFOREACH(COMPONENT)

IF(NOT FFMPEG_FIND_COMPONENTS)
    SET(FFMPEG_FIND_COMPONENTS ${FFMPEG_COMPONENTS})
ENDIF(NOT FFMPEG_FIND_COMPONENTS)

SET(FFMPEG_FOUND "YES")
FOREACH(COMPONENT ${FFMPEG_FIND_COMPONENTS})
    STRING(TOLOWER ${COMPONENT} LOWERCOMPONENT)
    STRING(TOUPPER ${COMPONENT} UPPERCOMPONENT)


    IF(FFMPEG_${UPPERCOMPONENT}_LIBRARY AND FFMPEG_${UPPERCOMPONENT}_INCLUDE_DIR)
        LIST(APPEND FFMPEG_LIBRARIES ${FFMPEG_${UPPERCOMPONENT}_LIBRARY})
        LIST(APPEND FFMPEG_INCLUDE_DIRS ${FFMPEG_${UPPERCOMPONENT}_INCLUDE_DIR})
        MESSAGE(STATUS "found lib${LOWERCOMPONENT}: -I${FFMPEG_${UPPERCOMPONENT}_INCLUDE_DIR}, ${FFMPEG_${UPPERCOMPONENT}_LIBRARY}")
    ELSE(FFMPEG_${UPPERCOMPONENT}_LIBRARY AND FFMPEG_${UPPERCOMPONENT}_INCLUDE_DIR)
        SET(FFMPEG_FOUND "NO")
        IF(FFMPEG_FIND_REQUIRED)
            MESSAGE(FATAL_ERROR "Required ffmpeg component ${LOWERCOMPONENT} NOT found")
        ELSE(FFMPEG_FIND_REQUIRED)
            MESSAGE(STATUS "ffmpeg component ${LOWERCOMPONENT} NOT found")
        ENDIF(FFMPEG_FIND_REQUIRED)
    ENDIF(FFMPEG_${UPPERCOMPONENT}_LIBRARY AND FFMPEG_${UPPERCOMPONENT}_INCLUDE_DIR)
ENDFOREACH(COMPONENT)

IF(FFMPEG_INCLUDE_DIRS)
    LIST(REMOVE_DUPLICATES FFMPEG_INCLUDE_DIRS)
ENDIF(FFMPEG_INCLUDE_DIRS)

MARK_AS_ADVANCED(FFMPEG_LIBRARIES FFMPEG_INCLUDE_DIRS FFMPEG_FOUND)
